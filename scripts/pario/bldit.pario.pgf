#! /bin/csh -f

# RCS file, release, date & time of last delta, author, state, [and locker]
# $Header: /project/work/rep/SCRIPTS/src/pario/bldit.pario.pgf,v 1.3 2006/03/10 21:17:27 sjr Exp $ 

# ====================== PARIOv4.5.1 Build Script =================== #
# Usage: bldit.pario.pgf >&! bldit.pario.pgf.log                      #
# Requirements: I/O API include files, CVS, and PGI Fortran compiler, #
#               MPICH include  files                                  #
# Note that this script is configured/tested for Red Hat Linux O/S    #
# The following environment variables must be set for this script to  #
# build an executable.                                                #
#   setenv M3MODEL =  source code CVS archive                         #
#   setenv M3LIB   =  code libraries                                  #
# To report problems or request help with this script/program:        #
#             http://www.cmascenter.org/html/help.html                #
# =================================================================== #

## Check for M3MODEL and M3LIB settings:
 if ( ! -e $M3MODEL || ! -e $M3LIB ) then
    echo "   $M3MODEL or $M3LIB directory not found"
    exit 1
    endif
 echo "   Model archive path: $M3MODEL"
 echo "            LIBS path: $M3LIB"
 set echo
 set BLD_OS = `uname -s`         ## Script set up for Linux only
 if ($BLD_OS != 'Linux') then
    echo "   $BLD_OS -> wrong bldit script for host\!"
    exit 1
    endif

#:#:#:#:#:#:#:#:#:#:#:Begin User Input Section #:##:#:#:#:#:#:#:#:#:#:#

 set BASE = $cwd
 set DEST = ${M3LIB}/pario/${BLD_OS}   # location of output library files
 if ( ! -d "$DEST" ) mkdir -p $DEST
 set RM = "/bin/rm -f"
 set MV = "/bin/mv -f"

 set MPI   = /share/linux/bin/mpich-ch_p4/include
 set IOAPI = $M3LIB/ioapi_3/ioapi/fixed_src

#> Set full path of Fortran 90 compiler
 set FC = /share/linux/pgi/linux86/5.0/bin/pgf90
 set FSTD = "-Mfixed -Mextend -c"
# set DEBUG     # Uncomment to create debug version of library
 if ( $?DEBUG ) then
    set F_FLAGS = "${FSTD} -g -I${MPI} -I${IOAPI}"
 else
    set F_FLAGS = "${FSTD} -O2 -I${MPI} -I${IOAPI}"
 endif

#:#:#:#:#:#:#:#:#:#:#:# End of User Input Section :#:#:#:#:#:#:#:#:#:#:#:#:#

 setenv CVSROOT $M3MODEL/PARIO

 set Bld = $BASE/BLD
 if ( ! -e "$Bld" ) then
    mkdir $Bld
    else
    if ( ! -d "$Bld" ) then
       echo "   *** target exists, but not a directory ***"
       exit 1
    endif
 endif

 cd $Bld

 echo " "
 echo "Host: " `hostname`
 echo "Host operating system:       $BLD_OS"
 echo "Libraries/objects/f90-modules directory: $DEST"
 echo " "

 if ( $?DEBUG ) then
    set Arc = libpario.debug.a
    else
    set Arc = libpario.a
    endif
 if ( -e "$Arc" ) $RM $Arc *.mod

#> extract source files from cvs archive
 cvs export -r HEAD -d $Bld PARIO

 set flist = (\
      alloc_data_mod.f\
      pinterpb_mod.f\
      piomaps_mod.f\
      boundary.f\
      get_write_map.f\
      growbuf.f\
      gtndxhdv.f\
      initm3io.f\
      interpol.f\
      pinterpb.f\
      pio_init.f\
      pio_re_init.f\
      pio_end.f\
      pm3err.f\
      pm3exit.f\
      pm3warn.f\
      pshut3.f\
      pwrite3.f\
      pwrgrdd.f\
      readbndy.f\
      subdmap.f\
      wrsubmap.f )

#> compile flist
 foreach file ( $flist )
    $FC $F_FLAGS $file
    end

 ar rv $Arc *.o
 chmod 444 $Arc
 if ( -e "$DEST/$Arc" ) $MV $DEST/$Arc $DEST/$Arc.old
 $MV $Arc $DEST
 $MV *.mod $DEST

 if ( $?DEBUG ) then
    $MV *.o $DEST
    else
    $RM *.o
    endif
 echo " "; echo " Finish building $DEST/$Arc "
 exit()
