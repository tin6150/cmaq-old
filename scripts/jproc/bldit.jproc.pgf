#! /bin/csh -f

# RCS file, release, date & time of last delta, author, state, [and locker]
# $Header: /project/work/rep/SCRIPTS/src/jproc/bldit.jproc.pgf,v 1.3 2006/03/10 21:28:15 sjr Exp $ 

# ======================= JPROCv4.5.1 Build Script ================== #
# Usage: bldit.jproc.pgf >&! bldit.jproc.pgf.log                      #
# Requirements: I/O API & netCDF libs, CVS, and PGI Fortran compiler  #
# Note that this script is configured/tested for Red Hat Linux O/S    #
# The following environment variables must be set for this script to  #
# build an executable.                                                #
#   setenv M3MODEL =  source code CVS archive                         #
#   setenv M3LIB   =  code libraries                                  #
# To report problems or request help with this script/program:        #
#             http://www.cmascenter.org/html/help.html                #
# =================================================================== #

## Check for M3MODEL and M3LIB settings:
 if ( ! -e $M3MODEL || ! -e $M3LIB ) then
    echo "   $M3MODEL or $M3LIB directory not found"
    exit 1
    endif
 echo "   Model archive path: $M3MODEL"
 echo "           Tools path: $M3LIB"
 set BLD_OS = `uname -s`       ## Script set up for Linux only 
 if ($BLD_OS != 'Linux') then
    echo "   $BLD_OS -> wrong makit script for host!"
    exit 1
    endif
 set echo

#:#:#:#:#:#:#:#:#:#:#:Begin User Input Section #:##:#:#:#:#:#:#:#:#:#:#

#> user choices: cvs archives
 set Project = $M3MODEL/JPROC
 set GlobInc = $M3MODEL/include/release

#> user choices: base working directory
 set Base = $cwd

 set APPL  = d1a
 set CFG   = cfg.$APPL
 set MODEL = JPROC_$APPL

#> user choices: m3bld command, see M3BLD_README for options
 set Opt = verbose      # show requested commands as they are executed
#set MakeOpt            # builds a Makefile, but does not execute it

#> user choices: various modules

 set Revision = release      # release = latest CVS revision
#set Revision = '"CMAQv4_5_1"'

 set ModDriver = ( module jproc_table    $Revision; )

#> user choices: mechanism, check $GlobInc directory for all mechanism options
#set Mechanism = cb4_ae3_aq
 set Mechanism = cb4_ae4_aq
#set Mechanism = saprc99_ae3_aq
#set Mechanism = saprc99_ae4_aq

#> user choices: computing system configuration:
#>    name of the "BLD" directory for checking out and compiling source code
#>    compiler name and location/link flags
#>    library paths

 set Bld = $Base/BLD

#> Set full path of Fortran 90 compiler
 set FC = /share/linux/pgi/linux86/5.0/bin/pgf90
 set FP = $FC

#> Set compiler flags
 set FSTD       = "-Mfixed -Mextend"
 set F_FLAGS    = "${FSTD} -O2 -I."
 set CPP_FLAGS  = ""
 set C_FLAGS    = "-v -O2 -I."
 set LINK_FLAGS = "-Bstatic"

#> Set location of M3Bld executable
 set Blder = $M3LIB/build/$BLD_OS/m3bld

#> Set location of libraries
 set IOAPI  = "${M3LIB}/ioapi_3/${BLD_OS}2_x86pg -lioapi"
 set NETCDF = "${M3LIB}/netCDF/${BLD_OS} -lnetcdf"

#:#:#:#:#:#:#:#:#:#:#:# End of User Input Section :#:#:#:#:#:#:#:#:#:#:#:#:#

 if ( ! -e "$Bld" ) then
    mkdir $Bld
    else
    if ( ! -d "$Bld" ) then
       echo "   *** target exists, but not a directory ***"
       exit 1
       endif
    endif
 cd $Bld

#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#

 set LIB1 = "-L${IOAPI}"
 set LIB2 = "-L${NETCDF}"
 set LIBS = "$LIB1 $LIB2"

 set ICL_CONST = $GlobInc
 set ICL_MECH  = $GlobInc/$Mechanism

 setenv CVSROOT $Project

#> make the config file

 set Cfile = ${CFG}.bld
 set quote = '"'

 echo                                                               > $Cfile
 echo "model       $MODEL;"                                        >> $Cfile
 echo                                                              >> $Cfile
 echo "FPP         $FP;"                                           >> $Cfile
 echo                                                              >> $Cfile
 set text = "$quote$CPP_FLAGS$quote;"
 echo "cpp_flags   $text"                                          >> $Cfile
 echo                                                              >> $Cfile
 echo "f_compiler  $FC;"                                           >> $Cfile
 echo                                                              >> $Cfile
 echo "f_flags     $quote$F_FLAGS$quote;"                          >> $Cfile
 echo                                                              >> $Cfile
 echo "link_flags  $quote$LINK_FLAGS$quote;"                       >> $Cfile
 echo                                                              >> $Cfile
 echo "libraries   $quote$LIBS$quote;"                             >> $Cfile
 echo                                                              >> $Cfile
 echo "global      $Opt;"                                          >> $Cfile
 echo                                                              >> $Cfile

 set text="// mechanism:"
 echo "$text ${Mechanism}"                                         >> $Cfile
 echo "// project archive: ${Project}"                             >> $Cfile
 echo                                                              >> $Cfile

 echo "include SUBST_CONST      $ICL_CONST/CONST.EXT;"             >> $Cfile
 echo "include SUBST_RXCMMN     $ICL_MECH/RXCM.EXT;"               >> $Cfile
 echo "include SUBST_RXDATA     $ICL_MECH/RXDT.EXT;"               >> $Cfile

 echo "$ModDriver"                                                 >> $Cfile
 echo                                                              >> $Cfile

#> make the model executable

 if ( $?MakeOpt ) then
    $Blder -make $Cfile   # $Cfile = ${CFG}.bld
    else
    set NoMake
    $Blder $Cfile
    endif
 if ( $status != 0 ) then
    echo "   *** failure in $Blder ***"
    exit 1
    endif
 if ( -e "$Base/${CFG}" ) then
    echo "   >>> previous ${CFG} exists, re-naming to ${CFG}.old <<<"
    unalias mv
    mv $Base/${CFG} $Base/${CFG}.old
    endif
 cp ${CFG}.bld $Base/${CFG}
 if ( ( $Opt != no_compile ) && \
      ( $Opt != no_link    ) && \
      ( $Opt != parse_only ) && \
      ( $Opt != show_only  ) && \
        $?NoMake ) then
    mv $MODEL $Base
    endif

 exit
