#! /bin/csh -f

# RCS file, release, date & time of last delta, author, state, [and locker]
# $Header: /project/work/rep/SCRIPTS/src/build/bldit.m3bld,v 1.4 2006/03/10 21:15:56 sjr Exp $ 

# ======================= M3BLDv4.5.1 Build Script ================== #
# Usage: bldit.m3bld >&! bldit.m3bld.log                              #
# Requirements: CVS, and Gnu C compiler                               #
# Note that this script is configured/tested for Red Hat Linux O/S    #
# The following environment variables must be set for this script to  #
# build an executable.                                                #
#   setenv M3MODEL =  source code CVS archive                         #
#   setenv M3LIB   =  code libraries                                  #
# To report problems or request help with this script/program:        #
#             http://www.cmascenter.org/html/help.html                #
# =================================================================== #

## Check for M3MODEL and M3LIB settings:
 if ( ! -e $M3MODEL || ! -e $M3LIB ) then
    echo "   $M3MODEL or $M3LIB directory not found"
    exit 1
    endif
 echo "   Model archive path: $M3MODEL"
 echo "           Tools path: $M3LIB"

 set BLD_OS = `uname -s`        ## Script set up for Linux only
 if ($BLD_OS != 'Linux') then
    echo "   $BLD_OS -> wrong bldit script for host\!"
    exit 1
    endif
 set DEST = $M3LIB/build/${BLD_OS}

 set echo

 set BASE = $cwd  # cannot be $M3LIB/BUILD
 set EXEC = m3bld

# code repository
 setenv CVSROOT $M3MODEL/BUILD
 set Rel = HEAD

# set c compiler and flags
 set CC = /usr/bin/gcc
 set CFLAGS = " "

#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#

 set Bld = $BASE/BLD
#unset echo
 if ( ! -e "$Bld" ) then
    mkdir $Bld
    else
    if ( ! -d "$Bld" ) then
       echo "   *** target exists, but not a directory ***"
       exit 1
       else
       echo "Deleting files in $Bld"
       /bin/rm $Bld/*
       endif
    endif
#set echo
 cd $Bld

# extract source files from cvs archive
 cvs export -r $Rel -d $Bld includes
 cvs export -r $Rel -d $Bld m3bld

 $CC $CFLAGS *.c -o $EXEC
 if( ! -e $EXEC ) then
  echo " "; echo " ***ERROR*** Compile failed"; echo " "
  exit 1
  endif

 if ( ! -d "$DEST" ) mkdir -p $DEST
 cd $DEST
 if ( -e $EXEC ) then
    mv -f $EXEC $EXEC.old
    endif

 mv ${Bld}/${EXEC} $EXEC
 chmod 755 $EXEC

 echo " "; echo " Finish building $EXEC "

 exit()
