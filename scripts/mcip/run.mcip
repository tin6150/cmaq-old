#!/bin/csh -f 

# RCS file, release, date & time of last delta, author, state, [and locker]
# $Header: /project/work/rep/MCIP2/src/mcip2/run.mcip,v 1.6 2006/03/01 20:15:01 tlotte Exp $

#========================= MCIPv3.1 Run Script ==========================
#
#  Script:  run.mcip
#  Purpose: Runs Models-3/CMAQ Meteorology-Chemistry Interface
#           Processor.  Part of the US EPA's Models-3/CMAQ system.
#  Method:  In UNIX/Linux:  run.mcip >&! mcip.log
#  Revised: 20 Sep 2001  Original version.  (T. Otte)
#           18 Oct 2001  Added CoordName to user definitions.  Deleted
#                        script variable DomIdMM5.  Added Fortran link
#                        for GRIDDESC file.  Moved namelist output to
#                        WorkDir, and mmheader output to OutDir.  Added
#                        user variables I0, J0, NCOLS, and NROWS for
#                        MCIP windowing.  (T. Otte)
#           29 Jan 2002  Added new namelist for file names.  Generalized
#                        the end-of-namelist delimiter.  (T. Otte)
#           27 Feb 2002  Removed minimum size for windows.  (T. Otte)
#           19 Mar 2002  Changed default grid cell for printing.
#                        (T. Otte)
#           11 Jun 2003  Clarified instructions on use of BTRIM and
#                        setting I0 and J0 for windowing option.
#                        Removed GRIDBDY2D, GRIDBDY3D, and METBDY2D
#                        from output.  (T. Otte)
#           01 Jul 2004  Restored GRIDBDY2D to output.  (T. Otte)
#           29 Nov 2004  Added TERRAIN option for input to get
#                        fractional land use from MM5 preprocessor.
#                        (T. Otte)
#           26 May 2005  Changed I0 and J0 to Y0 and X0 to make code
#                        more general.  Removed "_G1" from environment
#                        variables for output files.  Created two new
#                        user options for calculating dry deposition
#                        velocities.  Added capability to process more
#                        than five input meteorology files in a single
#                        MCIP run.  (T. Otte)
#           27 Feb 2006  Updated automated namelist generator for
#                        Linux on Mac (assumed to be) using the XLF
#                        compiler.  (T. Otte)
#
#=======================================================================

#-----------------------------------------------------------------------
# Set identification for input and output files.
#
#   APPL       = Application Name (tag for MCIP output file names)
#   CoordName  = Coordinate system name for GRIDDESC
#   GridName   = Grid Name descriptor for GRIDDESC
#   InMetDir   = Directory that contains input meteorology files
#   InTerDir   = Directory that contains input MM5 "TERRAIN" file
#                (Used for providing fractional land-use categories,
#                and it will only work if IEXTRA was set to TRUE in
#                MM5's TERRAIN program.  Is TRUE for P-X simulations.)
#   OutDir     = Directory to write MCIP output files
#   ProgDir    = Directory that contains the MCIP executable
#   WorkDir    = Working Directory for Fortran links and namelist
#-----------------------------------------------------------------------

set APPL       = benchmark2005 
set CoordName  = LAM_40N100W         # 16-character maximum
set GridName   = M_32_99NASH      # 16-character maximum

set DataPath   = $M3DATA
set InMetDir   = $DataPath/raw/mm5
set InTerDir   = $InMetDir
set OutDir     = $DataPath/mcip3/$GridName
set ProgDir    = $cwd
set WorkDir    = $OutDir

#-----------------------------------------------------------------------
# Set name(s) of input meteorology file(s)
#
#   File name(s) must be set inside parentheses since "InMetFiles" is
#   a C-shell script array.  Multiple file names should be space-
#   delimited.  Additional lines can be used when separated by a
#   back-slash (\) continuation marker.  The file names can be as
#   they appear on your system; MCIP will link the files in by a
#   Fortran unit number and the explicit name via a namelist.  The
#   files must be listed in chronological order.  The maximum number
#   of input meteorology files must be less than or equal to the number
#   in MAX_MM in file_mod.F (default is 100).
#
#   Example:
#     set InMetFiles = ( $InMetDir/MMOUT_DOMAIN2.time1 \
#                        $InMetDir/MMOUT_DOMAIN2.time2 )
#
#-----------------------------------------------------------------------

set InMetFiles = ( $InMetDir/MMOUT_DOMAIN1.subset )

set IfTer      = "T"
set InTerFile  = $InTerDir/TERRAIN_DOMAIN1.subset

#-----------------------------------------------------------------------
# Set user control variables for MCIP execution.
#
#   LPBL:  1 = Use PBL values from input meteorology
#          2 = Re-calculate values using PBL similarity
#          3 = Re-calculate values using only surface layer similarity
#
#   LRAD:  1 = Use radiation fields from input meteorology
#          2 = Re-calculate fields from MCIP Version 1 algorithm
#
#   LDDEP: 1 = Use RADM (Wesely) dry deposition routine 
#          2 = Use Models-3 (Pleim) dry deposition routine     
#          3 = Use Models-3 dry deposition routine with extra Cl
#          4 = Use Models-3 dry deposition routine with extra Cl and Hg
#-----------------------------------------------------------------------

set LPBL  = 1
set LRAD  = 1
set LDDEP = 4

#-----------------------------------------------------------------------
# Set run start and end date.  (YYYY-MO-DD-HH:MI:SS.SSSS)
#   MCIP_START:  First date and time to be output [UTC]
#   MCIP_END:    Last date and time to be output  [UTC]
#   INTVL:       Frequency of output [minutes]
#-----------------------------------------------------------------------

set MCIP_START = 1999-07-02-00:00:00.0000  # [UTC]
set MCIP_END   = 1999-07-04-00:00:00.0000  # [UTC]

set INTVL      = 60 # [min]

#-----------------------------------------------------------------------
# Set CTM layers.  Should be in descending order starting at 1 and 
# ending with 0.  There is currently a maximum of 100 layers allowed.
# To use all of the layers from the input meteorology without
# collapsing (or explicitly specifying), set CTMLAYS = -1.0.
#-----------------------------------------------------------------------

#set CTMLAYS = "-1.0"

set CTMLAYS = "1.000, 0.980, 0.930, 0.840, 0.600, 0.300, 0.000"

#set CTMLAYS = "1.000, 0.995, 0.990, 0.980, 0.960, 0.940, 0.910, 0.860, \
#               0.800, 0.740, 0.650, 0.550, 0.400, 0.200, 0.000"

#set CTMLAYS = "1.000, 0.995, 0.985, 0.970, 0.950, 0.930, 0.910,  \
#               0.880, 0.840, 0.800, 0.740, 0.650, 0.550, 0.400,  \
#               0.200, 0.000"

#set CTMLAYS = "1.000, 0.995, 0.990, 0.980, 0.970, 0.960, 0.940,  \
#               0.920, 0.900, 0.880, 0.860, 0.840, 0.820, 0.800,  \
#               0.770, 0.740, 0.700, 0.650, 0.600, 0.500, 0.400,  \
#               0.300, 0.200, 0.100, 0.000"

#set CTMLAYS = "1.000, 0.995, 0.990, 0.985, 0.980, 0.970, 0.960,  \
#               0.950, 0.940, 0.930, 0.920, 0.910, 0.900, 0.880,  \
#               0.860, 0.840, 0.820, 0.800, 0.770, 0.740, 0.700,  \
#               0.650, 0.600, 0.550, 0.500, 0.450, 0.400, 0.350,  \
#               0.300, 0.250, 0.200, 0.150, 0.100, 0.050, 0.000"

#-----------------------------------------------------------------------
# Determine whether or not static output (GRID) files will be created.
#-----------------------------------------------------------------------

set MKGRID = T

#-----------------------------------------------------------------------
# Set number of meteorology "boundary" points to remove on each of four
# horizontal sides of MCIP domain.  This affects the output MCIP domain
# dimensions by reducing meteorology domain by 2*BTRIM + 2*NTHIK + 1,
# where NTHIK is the lateral boundary thickness (in BDY files), and the
# extra point reflects conversion from grid points (dot points) to grid
# cells (cross points).  Setting BTRIM = 0 will use maximum of input
# meteorology.  To remove MM5 lateral boundaries, set BTRIM = 5.
#
# *** If windowing a specific subset domain of input meteorology, set
#     BTRIM = -1, and BTRIM will be ignored in favor of specific window
#     information in X0, Y0, NCOLS, and NROWS.
#-----------------------------------------------------------------------

set BTRIM = 0

#-----------------------------------------------------------------------
# Define MCIP subset domain.  (Only used if BTRIM = -1.  Otherwise,
# the following variables will be set automatically from BTRIM and
# size of input meteorology fields.)
#   X0:     X-coordinate of lower-left corner of full MCIP "X" domain
#           (including MCIP lateral boundary) based on input MM5 domain.
#           X0 refers to the east-west dimension.  Minimum value is 1.
#   Y0:     Y-coordinate of lower-left corner of full MCIP "X" domain
#           (including MCIP lateral boundary) based on input MM5 domain.
#           Y0 refers to the north-south dimension.  Minimum value is 1.
#   NCOLS:  Number of columns in output MCIP domain (excluding MCIP
#           lateral boundaries).
#   NROWS:  Number of rows in output MCIP domain (excluding MCIP
#           lateral boundaries).
#-----------------------------------------------------------------------

set X0    =   6
set Y0    =   6
set NCOLS = 148
set NROWS = 112

#-----------------------------------------------------------------------
# Set coordinates for cell for diagnostic prints on output domain.
# If coordinate is set to 0, domain center cell will be used.
#-----------------------------------------------------------------------

set LPRT_COL = 0
set LPRT_ROW = 0

#=======================================================================
#=======================================================================
# Set up and run MCIP.
#   Should not need to change anything below here.
#=======================================================================
#=======================================================================

set PROG = mcip

date

#-----------------------------------------------------------------------
# Make sure directories exist.
#-----------------------------------------------------------------------

if ( ! -d $InMetDir ) then
  echo "No such input directory $InMetDir"
  exit 1
endif

if ( ! -d $OutDir ) then
  echo "No such output directory...will try to create one"
  mkdir -p $OutDir
  if ( $status != 0 ) then
    echo "Failed to make output directory, $OutDir"
    exit 1
  endif
endif

if ( ! -d $ProgDir ) then
  echo "No such program directory $ProgDir"
  exit 1
endif

#-----------------------------------------------------------------------
# Make sure the input files exist.
#-----------------------------------------------------------------------

if ( $IfTer == "T" ) then
  if ( ! -f $InTerFile ) then
    echo "No such input file $InTerFile"
    exit 1
  endif
endif

foreach fil ( $InMetFiles )
  if ( ! -f $fil ) then
    echo "No such input file $fil"
    exit 1
  endif
end

#-----------------------------------------------------------------------
# Make sure the executable exists.
#-----------------------------------------------------------------------

if ( ! -f $ProgDir/${PROG}.exe ) then
  echo "Could not find ${PROG}.exe"
  exit 1
endif

#-----------------------------------------------------------------------
# Create a work directory for this job.
#-----------------------------------------------------------------------

if ( ! -d $WorkDir ) then
  mkdir -p $WorkDir
  if ( $status != 0 ) then
    echo "Failed to make work directory, $WorkDir"
    exit 1
  endif
endif

cd $WorkDir

#-----------------------------------------------------------------------
# Set up script variables for input files.
#-----------------------------------------------------------------------

if ( $IfTer == "T" ) then
  if ( -f $InTerFile ) then
    set InTer = $InTerFile
  else
    set InTer = "no_file"
  endif
else
  set InTer = "no_file"
endif

set FILE_GD  = $OutDir/GRIDDESC
set FILE_HDR = $OutDir/mmheader.${APPL}

#-----------------------------------------------------------------------
# Create namelist with user definitions.
#-----------------------------------------------------------------------

set MACHTYPE = `uname`
if ( ( $MACHTYPE == "AIX" ) || ( $MACHTYPE == "Darwin" ) ) then
  set Marker = "/"
else
  set Marker = "&END"
endif

cat > $WorkDir/namelist.${PROG} << !

 &FILENAMES
  file_gd    = "$FILE_GD"
  file_hdr   = "$FILE_HDR"
  file_mm    = "$InMetFiles[1]",
!

if ( $#InMetFiles > 1 ) then
  @ nn = 2
  while ( $nn <= $#InMetFiles )
    cat >> $WorkDir/namelist.${PROG} << !
               "$InMetFiles[$nn]",
!
    @ nn ++
  end
endif

if ( $IfTer == "T" ) then
cat >> $WorkDir/namelist.${PROG} << !
  file_ter   = "$InTer"
!
endif

cat >> $WorkDir/namelist.${PROG} << !
  makegrid   = .${MKGRID}.
 $Marker

 &USERDEFS
  lpbl       =  $LPBL
  lrad       =  $LRAD
  lddep      =  $LDDEP
  mcip_start = "$MCIP_START"
  mcip_end   = "$MCIP_END"
  intvl      =  $INTVL
  coordnam   = "$CoordName"
  grdnam     = "$GridName"
  ctmlays    =  $CTMLAYS
  btrim      =  $BTRIM
  lprt_col   =  $LPRT_COL
  lprt_row   =  $LPRT_ROW
 $Marker

 &WINDOWDEFS
  x0         =  $X0
  y0         =  $Y0
  ncolsin    =  $NCOLS
  nrowsin    =  $NROWS
 $Marker

!

#-----------------------------------------------------------------------
# Set links to FORTRAN units.
#-----------------------------------------------------------------------

rm fort.*
if ( -f $FILE_GD ) rm -f $FILE_GD

ln -s $FILE_HDR                  fort.2
ln -s $FILE_GD                   fort.4
ln -s $WorkDir/namelist.${PROG}  fort.8
if ( $IfTer == "T" ) then
  ln -s $InTerFile               fort.9
endif

set NUMFIL = 0
foreach fil ( $InMetFiles )
  @ NN = $NUMFIL + 10
  ln -s $fil fort.$NN
  @ NUMFIL ++
end

#-----------------------------------------------------------------------
# Set output file names and other miscellaneous environment variables.
#-----------------------------------------------------------------------

setenv IOAPI_CHECK_HEADERS  T
setenv EXECUTION_ID         $PROG

setenv GRID_BDY_2D          $OutDir/GRIDBDY2D_${APPL}
setenv GRID_CRO_2D          $OutDir/GRIDCRO2D_${APPL}
setenv GRID_CRO_3D          $OutDir/GRIDCRO3D_${APPL}
setenv GRID_DOT_2D          $OutDir/GRIDDOT2D_${APPL}
setenv MET_BDY_3D           $OutDir/METBDY3D_${APPL}
setenv MET_CRO_2D           $OutDir/METCRO2D_${APPL}
setenv MET_CRO_3D           $OutDir/METCRO3D_${APPL}
setenv MET_DOT_3D           $OutDir/METDOT3D_${APPL}

#-----------------------------------------------------------------------
# Execute MCIP.
#-----------------------------------------------------------------------

$ProgDir/${PROG}.exe

if ( $status == 0 ) then
  rm fort.*
  exit 0
else
  echo "Error running $PROG"
  exit 1
endif
